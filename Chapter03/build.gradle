buildscript {
    ext.kotlin_version = '1.2.41'
    repositories {
        jcenter()
        mavenCentral()
        google()
        maven {
            url "https://dl.bintray.com/jetbrains/kotlin-native-dependencies"
        }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:3.1.2"
        classpath "org.jetbrains.kotlin:kotlin-native-gradle-plugin:0.6.2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

repositories {
    jcenter()
}

apply plugin: 'konan'
apply plugin: 'com.android.application'

konan.targets = ['android_arm32', 'android_arm64']

def outDir = file('handson')
def libsDir = file("$outDir/libs")

def platforms = [
        "armeabi-v7a": [konanTarget: "android_arm32"],
        "arm64-v8a"  : [konanTarget: "android_arm64"]
]

konanArtifacts {
    program('handson') {
        artifactName 'libhandson_screen'
    }
}

task copyLibs(type: Copy) {
    dependsOn konanArtifacts.handson
    destinationDir libsDir

    platforms.each { name, platform ->
        into(name) {
            from konanArtifacts.handson."${platform.konanTarget}".artifact
        }
    }
}

task deleteOut(type: Delete) {
    delete outDir
}

clean.dependsOn deleteOut

tasks.matching { it.name == 'preBuild' }.all {
    it.dependsOn copyLibs
}

android {
    compileSdkVersion 25

    defaultConfig {
        applicationId 'com.example.handson'
        minSdkVersion 9
        targetSdkVersion 25

        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a"
        }
    }

    sourceSets {
        main {
            jniLibs.srcDir libsDir
        }
        main.java.srcDirs += 'src/main/kotlin'
    }
}

task buildApk(type: Copy) {
    dependsOn "assembleDebug"
    destinationDir outDir
    from 'build/outputs/apk'
}
